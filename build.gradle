buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    flatDir {
        dirs 'prebuilt-libs'
    }
}

android {
    compileSdkVersion "Google Inc.:Glass Development Kit Preview:19"
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "walker.blue.glass.app"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def checkLocal(String remote, String local) {
    if (rootProject.subprojects.toString().contains(local)) {
        println "[checkLocal] Using local dependency ${local} for project ${project.name}"
        project(local)
    } else {
        println "[checkLocal] Using remote dependency ${remote} for project ${project.name}"
        remote
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile checkLocal('blue.walker:beacon-lib:0.1.+', ':Libraries:BeaconLib')
    compile checkLocal('blue.walker:core:0.1.+', ':Libraries:Core')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}